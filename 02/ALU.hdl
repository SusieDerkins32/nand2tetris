// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input? - complement or invert
        zy, // zero the y input?
        ny, // negate the y input? - complement or invert
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
		// Process x, using zx and nx
		Mux16(a=x, b=false, sel=zx, out=t1x);
		Not16(in=t1x, out=t2x);
		Mux16(a=t1x, b=t2x, sel=nx, out=t3x);
		
		// Process y, using zy and ny
		Mux16(a=y, b=false, sel=zy, out=t1y);
		Not16(in=t1y, out=t2y);
		Mux16(a=t1y, b=t2y, sel=ny, out=t3y);
		
		// process f
		And16(a=t3x, b=t3y, out=xandy);
		Add16(a=t3x, b=t3y, out=xplusy);
		Mux16(a=xandy, b=xplusy, sel=f, out=t1out);
		
		// process no
		Not16(in=t1out, out=t2out);
		Mux16(a=t1out, b=t2out, sel=no, out[0..7]=outLSB, out[8..14]=outMSB, out[15]=outMSBbit);
		Mux16(a[0..7]=outLSB, a[8..14]=outMSB, a[15]=outMSBbit, b=false, sel=false, out=out);
		
		// calculate zr
		Or8Way(in=outLSB, out=t1zr);
		Or8Way(in[0..6]=outMSB, in[7]=outMSBbit, out=t2zr);
		Or(a=t1zr, b=t2zr, out=t3zr);
		Not(in=t3zr, out=zr);
		
		// calculate ng
		And(a=outMSBbit, b=true, out=ng);
}
